# Contributor: noonov <noonov@gmail.com>

pkgname=nkf-git
pkgver=v2_1_5.r13.ga4f55f2
pkgrel=1
pkgdesc="A yet another kanji code converter among networks, hosts and terminals"
arch=('any')
url="https://osdn.net/projects/nkf/"
license=('custom')
depends=('glibc')
conflicts=('nkf')
source=(git+https://git.osdn.net/gitroot/nkf/nkf.git
        LICENSE)
b2sums=('SKIP'
        'b3738fe113b4f8bb27be883a6a4bef1ca209fe9e6ac9ab519bb6b5cef5154f7617d03ec6f52a8b94a83789a5eafaab14fc7364f54792784203d43ead6606b1d6')

pkgver(){
    cd $srcdir/nkf
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${srcdir}/nkf
  sed -i "/^CFLAGS/s|$| $CFLAGS|" Makefile
  make
  ./nkf -w --overwrite nkf.1j
}

package() {
  cd ${srcdir}/nkf
  install -D -m755 nkf               "${pkgdir}/usr/bin/nkf"
  install -D -m644 nkf.1             "${pkgdir}/usr/share/man/man1/nkf.1"
  install -D -m644 nkf.1j            "${pkgdir}/usr/share/man/ja/man1/nkf.1"
  install -D -m644 ${srcdir}/LICENSE "${pkgdir}/usr/share/licenses/nkf/LICENSE"
}
