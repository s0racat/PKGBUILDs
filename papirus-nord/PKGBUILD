pkgname=papirus-nord
pkgver=3.2.0
pkgrel=1
epoch=1
pkgdesc="Custom colors for folder icons for Papirus Icon Theme!"
arch=('any')
url="https://www.pling.com/p/1360398"
license=('GPL2')
depends=('papirus-icon-theme')
options=('!strip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Adapta-Projects/Papirus-Nord/archive/refs/tags/$pkgver.tar.gz"
        "Make-usable-in-doas.patch")
sha256sums=('ffdb977c22630b5151e1f4956416ccd11a29394828509f36dab99ff7e243d119'
            'a3cff618aa97452f45f1f645323d1ce4912cd19d88512f3b8d88e31996857f27')

prepare() {
    cd Papirus-Nord-$pkgver
    patch -i $srcdir/Make-usable-in-doas.patch
}

package() {
  cd Papirus-Nord-$pkgver
  install -Dm755 papirus-folders $pkgdir/usr/bin/papirus-folders
  for i in 22 24 32 48 64; do
    install -d "$pkgdir/usr/share/icons/Papirus/${i}x${i}/places"
    cp -r Icons/${i}x${i}/* "$pkgdir/usr/share/icons/Papirus/${i}x${i}/places"
  done
}
